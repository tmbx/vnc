; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=MetaVNC
AppVerName=MetaVNC 0.6.6
AppVersion=0.6.6
AppPublisher=UCHINO Satoshi
AppPublisherURL=http://metavnc.sourceforge.net
AppSupportURL=http://metavnc.sourceforge.net
AppUpdatesURL=http://metavnc.sourceforge.net
DefaultDirName={pf}\MetaVNC
UsePreviousAppDir=false
DefaultGroupName=MetaVNC
UsePreviousGroup=false
AllowNoIcons=true
LicenseFile=licence.txt
InfoAfterFile=INFO-metavnc.rtf
Compression=zip/9
WindowVisible=false
DisableStartupPrompt=true
BackColor=clBlack
BackColor2=clBlue
Uninstallable=true

ChangesAssociations=true
ShowLanguageDialog=yes
MinVersion=0,5.0.2195
OutputBaseFilename=metavnc-win32-0.6.6
VersionInfoVersion=0.6.6

[Components]
Name: server; Description: MetaVNC Server; Types: full compact custom
Name: viewer; Description: MetaVNC Viewer; Types: full compact custom

[Files]
Source: winvnc\Release\WinVNC.exe; DestDir: {app}; Flags: ignoreversion restartreplace; Components: server
Source: winvnc\Release\VNCHooks.dll; DestDir: {app}; Flags: ignoreversion restartreplace; Components: server
Source: vncviewer\Release\vncviewer.exe; DestDir: {app}; Flags: ignoreversion; Components: viewer
Source: README.txt; DestDir: {app}; Flags: ignoreversion
Source: README-metavnc.txt; DestDir: {app}; Flags: ignoreversion
Source: LICENCE.txt; DestDir: {app}; Flags: ignoreversion
Source: MetaVNC.url; DestDir: {app}; Flags: ignoreversion
Source: msvcp71.dll; DestDir: {sys}; Flags: uninsneveruninstall onlyifdoesntexist
Source: msvcr71.dll; DestDir: {sys}; Flags: uninsneveruninstall onlyifdoesntexist

[Icons]
Name: {group}\Launch MetaVNC Server; FileName: {app}\WinVNC.exe; WorkingDir: {app}; Components: server
Name: {group}\Show User Settings; FileName: {app}\WinVNC.exe; Parameters: -settings; WorkingDir: {app}; Components: server
Name: {group}\MetaVNC Viewer; FileName: {app}\vncviewer.exe; WorkingDir: {app}; Components: viewer
Name: {group}\Administration\Install VNC Service; FileName: {app}\WinVNC.exe; Parameters: -install; WorkingDir: {app}; Components: server
Name: {group}\Administration\Remove VNC Service; FileName: {app}\WinVNC.exe; Parameters: -remove; WorkingDir: {app}; Components: server
Name: {group}\Administration\Run Service Helper; FileName: {app}\WinVNC.exe; Parameters: -servicehelper; WorkingDir: {app}; Components: server
Name: {group}\Administration\Show Default Settings; FileName: {app}\WinVNC.exe; Parameters: -defaultsettings; WorkingDir: {app}; Components: server
Name: {group}\Administration\Clean up remote menus of unterminated sessions; FileName: {app}\vncviewer.exe; Parameters: -cleanmenu; WorkingDir: {app}; Components: viewer
Name: {group}\Documentation\README - MetaVNC; FileName: {app}\README-metavnc.txt; WorkingDir: {app}
Name: {group}\Documentation\Licensing Terms; FileName: {app}\LICENCE.txt; WorkingDir: {app}
Name: {group}\Documentation\MetaVNC Web Site; FileName: {app}\MetaVNC.url

[Tasks]
Name: associate; Description: &Associate .vnc files with MetaVNC Viewer; GroupDescription: File associations:; Components: viewer
Name: installservice; Description: &Register new MetaVNC Server as a system service; GroupDescription: Server configuration:; Components: server; Flags: unchecked
Name: startservice; Description: &Start or restart MetaVNC service; GroupDescription: Server configuration:; Components: server; Flags: unchecked; MinVersion: 0,1

[Registry]
Root: HKCR; Subkey: .vnc; ValueType: string; ValueName: ; ValueData: VncViewer.Config; Flags: uninsdeletevalue; Tasks: associate
Root: HKCR; Subkey: VncViewer.Config; ValueType: string; ValueName: ; ValueData: VNCviewer Config File; Flags: uninsdeletekey; Tasks: associate
Root: HKCR; Subkey: VncViewer.Config\DefaultIcon; ValueType: string; ValueName: ; ValueData: {app}\vncviewer.exe,0; Tasks: associate
Root: HKCR; Subkey: VncViewer.Config\shell\open\command; ValueType: string; ValueName: ; ValueData: """{app}\vncviewer.exe"" -config ""%1"""; Tasks: associate

[Run]
Filename: {app}\WinVNC.exe; Parameters: -reinstall; Tasks: installservice
Filename: net; Parameters: start WinVNC; Tasks: startservice
Filename: {app}\WinVNC.exe; Parameters: -servicehelper; Flags: nowait; Tasks: startservice
